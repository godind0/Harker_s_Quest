// room script file
bool intro_opening = true;

function change_clothes()
{
  open_changeClothes_gui(true, true, true, true, true, false, 2); //(1_coat, 2_necktie, 3_shirt, 4_pants, 5_shoe, 6_hair)
}

function checkClothes()
{
  if(!player.isPlayerWearing(BP_coat, "Brown") && player.getWornClothesIndex(BP_coat) != -1)
  {
    player.Say("This coat might be a little too colorful for a normal day at the office");
  } else if(player.getWornClothesIndex(BP_necktie) == -1)
  {
    player.Say("I need to put on a tie");
  } else if(player.getWornClothesIndex(BP_pants) == -1)
  {
    player.Say("I need to put on some pants");
  } else if(player.getWornClothesIndex(BP_shoes) == -1)
  {
    player.Say("I need to put on shoes");
  } else if(player.getWornClothesIndex(BP_undershirt) == -1)
  {
    player.Say("I need to put on an undershirt");
  }    
}

bool highjackParseCommand(String cmd)
{
  bool sendBackToNormalParsing = true;
  if(cmd.IndexOf("clothes") != -1 || cmd.IndexOf("change") != -1)
  {
    if(isCharacterNearObject(oBrownCoat, 60.0, player))
    {
      createPlayerClothing(BP_coat, "Red", 47559, 41350, 24803, true);
      createPlayerClothing(BP_coat, "Brown", 16640, 20800, 8320, true);
      createPlayerClothing(BP_coat, "Yellow", 48551, 42214, 25380, true);
      createPlayerClothing(BP_coat, "Blue", 2875, 2775, 2544, true);
      createPlayerClothing(BP_coat, "Orange", 56353, 48001, 31297, true);
      
      createPlayerClothing(BP_pants, "Black", 4258, 4258, 32, true);      
      createPlayerClothing(BP_shoes, "Brown", 16640, 20800, 8320, true);     
      createPlayerClothing(BP_necktie, "Black", 4258, 4258, 32, true);
      createPlayerClothing(BP_undershirt, "White", 65535, 61277, 52825, true);
      change_clothes();
    }
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackIndexElementVerb(int index, elementType eT, String verb)
{
  bool sendBackToNormalParsing = true;
  
  return sendBackToNormalParsing;
}

bool highjackFailedInteract(String cmd)
{
  bool sendBackToNormalParsing = true;
  //do nothing
  return sendBackToNormalParsing;
}


function highjackAfterInteract(int index, elementType eT, String verb)
{
  //do nothing
}

function on_call(int index)
{
  String indexElementVerbHandled[];
  String cmd;
  int elementIndex;
  elementType eT;
  String verb;
  bool handled = false;
  
  switch(index)
  {
    case 1://highjack or interact
      indexElementVerbHandled = new String[3];
      cmd = tbParser.Text;
      hide_parser_gui();
      
      if(highjackParseCommand(cmd)) 
      {
        indexElementVerbHandled = roomSpecificParse(cmd);
        elementIndex = indexElementVerbHandled[0].AsInt;
        eT = stringToET(indexElementVerbHandled[1]);
        verb = indexElementVerbHandled[2];
        
        if(highjackIndexElementVerb(elementIndex, eT, verb))
        {
          if(verb != "Skip")
          {
            if(verb != "Null") 
            {
              handled = interact(indexElementVerbHandled);
            }
          } else {
            handled = true;
          }
          
          if(handled) {
            highjackAfterInteract(elementIndex, eT, verb);
          } else {
            if(highjackFailedInteract(cmd))
            {
              player.Think("Hmmm. . . I do not quite get what the goal is, here");
            }
          }
        }
      }
      break;
    case 2:
      checkClothes();
      break;
  }
}


function room_FirstLoad()
{
  setOpeningAndClosingTimes();
  setupTravelDestinations();

  addTravelDestination(JonathanApt);
  addTravelDestination(HawkinsOffices);
  
  setStoryBeat(startYourDay);
  setCurrentZone(JonathanApt);
  
  sit(roomObjects, 0, player, player.GetProperty("sitView"), true);
  
  Display("JONATHAN HARKERâ€™S JOURNAL[(Kept in shorthand.)[23 april. Exeter");
  String diaryContent = "23 april. Exeter. Just got assigned my 100th property contract tonight. This means that I fulfilled the last requirement before taking my sollicitor exam. I will have to plan a trip to London soon to visit the Law Society and arrange for it. This is most thrilling. Once the exam is over, I will be one major sale away from secure finances. Then, Mina and I can finally marry. That wedding will be a grand expensive one that will be the talk of London. It is late already but I cannot sleep. Maybe I will light a candle and work tonight at my home desk until I am done.";
  createBook("Diary", diaryContent, " ",  " ");
  show_book_gui("Diary");
}

function room_RepExec()
{
  if(intro_opening && !gBook.Visible)
  {
    FadeOut(5);
    gDark.Transparency = 100;
    FadeIn(5);
    triggerCutscene(beginQuest);
    intro_opening = false;
  }
}
