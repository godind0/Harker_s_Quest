// room script file
bool highjackParseCommand(String cmd)
{
  bool sendBackToNormalParsing = true;
  if((cmd.StartsWith("open", eCaseInsensitive) || cmd.StartsWith("use", eCaseInsensitive) || cmd.StartsWith("remove", eCaseInsensitive)) &&
     (cmd.IndexOf("door") != -1 || cmd.IndexOf("fence") != -1 || cmd.IndexOf("gate") != -1 || cmd.IndexOf("hinge") != -1) &&
     (cmd.IndexOf("knife") != -1 || cmd.IndexOf("blade") != -1))
  {
    if(player.HasInventory(iKnife))
    {
      player.Say("Maybe I can use Mina's knife to remove the hinges...");
      player.Walk(32, 139, eBlock, eWalkableAreas);
      player.FaceDirection(eDirectionUp);
      oFenceDoor.SetProperty("Close_SpriteNumber", 210);
      oFenceDoor.SetProperty("Open_SpriteNumber", -2);
      oFenceDoor.Visible = false;
      player.Say("Success! Thank you my love!");
    } else
    {
      player.Say("I'd need a blade to do that");
    }
  } else if((cmd.StartsWith("Knock", eCaseInsensitive) || cmd.StartsWith("Use", eCaseInsensitive)) && (cmd.IndexOf("Door") != -1))
  {
    sendBackToNormalParsing = false;
    if(isCharacterNearHotspot(hFrontDoor, 20.0, player))
    {
      player.Walk(hFrontDoor.WalkToX, hFrontDoor.WalkToY, eBlock, eWalkableAreas);
      player.FaceDirection(eDirectionUp, eBlock);
      Display("Knock, knock");
      Wait(80);
      player.Walk(player.x, player.y + 30, eBlock, eWalkableAreas);
      player.FaceDirection(eDirectionUp);
      player.Say("Hello?");
      Wait(80);
      player.FaceDirection(eDirectionRight);
      Wait(50);
      player.FaceDirection(eDirectionLeft);
      Wait(40);
      player.FaceDirection(eDirectionUp);
      player.Think("There is no one here");
      giveScoreOnce("Verify if someone is home", 1);
    } else
    {
      player.Say("I am too far from the door");
    }
  } else if ((cmd.StartsWith("look", eCaseInsensitive) || cmd.StartsWith("examine", eCaseInsensitive) || cmd.StartsWith("observe", eCaseInsensitive)) && (cmd.IndexOf("Mansion") != -1 || cmd.IndexOf("House") != -1 || cmd.IndexOf("Carfax") != -1))
  {
    player.Say("This is Carfax, no doubt a corruption of the old Quatre Face, as the house is four-sided, agreeing with the cardinal points of the compass");
    player.Say("The land must be in all some twenty acres, quite surrounded by a solid stone wall I cross without difficulty");
    player.Say("The house is very large and of all periods back, I should say, to medi√¶val times");
    player.Say("One part is of stone immensely thick, with a few windows. It looks like part of a keep");
    player.Say("The house has been added to, but in a very straggling way, and I can only guess at the amount of ground it covers, which must be very great");
    player.FaceDirection(eDirectionLeft);
    Wait(20);
    player.FaceDirection(eDirectionRight);
    player.Say("There are but few houses close at hand, one being a very large house only recently added to and formed into a private sanitarium");
    player.Say("It is not, however, visible from the grounds");
    giveScoreOnce("Observe Carfax", 3);
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackIndexElementVerb(int index, elementType eT, String verb)
{
  bool sendBackToNormalParsing = true;
  
  if(eT == roomHotspots && index == 1 && verb == "photograph")//dining
  {
    takeAPicture("Carfax's Dining", "This is the property's dining room", 1);
    sendBackToNormalParsing = false;
  } else if(eT == roomHotspots && index == 2 && verb == "photograph")//living
  {
    takeAPicture("Carfax's Living", "This is the property's living room", 1);
    sendBackToNormalParsing = false;
  } else if(eT == roomHotspots && index == 3)//door
  {
    player.Say("That door is in bad shape, I'd better leave it alone");
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackFailedInteract(String cmd)
{
  bool sendBackToNormalParsing = true;
  //insert highjacking formulas here (and set the bool to false)
  return sendBackToNormalParsing;
}


function highjackAfterInteract(int index, elementType eT, String verb)
{
  //insert highjacking formulas here (and set the bool to false)
}

function on_call(int index)
{
  String indexElementVerbHandled[];
  String cmd;
  int elementIndex;
  elementType eT;
  String verb;
  bool handled = false;
  
  switch(index)
  {
    case 1://highjack or interact
      indexElementVerbHandled = new String[3];
      cmd = tbParser.Text;
      close_gui(gParser);
      
      if(highjackParseCommand(cmd)) 
      {
        indexElementVerbHandled = roomSpecificParse(cmd);
        elementIndex = indexElementVerbHandled[0].AsInt;
        eT = stringToET(indexElementVerbHandled[1]);
        verb = indexElementVerbHandled[2];
        //debugDisplay(String.Format("Element type: %s[Index: %d[Verb: %s", etToString(eT), elementIndex, verb));
        
        if(highjackIndexElementVerb(elementIndex, eT, verb))
        {
          if(verb != "Skip")
          {
            if(verb != "Null") 
            {
              handled = interact(indexElementVerbHandled);
            }
          } else {
            handled = true;
          }
          
          if(handled) {
            highjackAfterInteract(elementIndex, eT, verb);
          } else {
            if(highjackFailedInteract(cmd))
            {
              player.Think("Hmmm... I do not quite get what the goal is, here");
            }
          }
        }
      }
      break;
  }
}

function room_AfterFadeIn()
{
  musicInRoom(aDeambulation);
  
}

function room_Load()
{
  if(player.x == 36 && player.y == 152)//door should be opened
  {
      oFenceDoor.Visible = false;
  }
}

function oFenceDoor_UseInv()
{
  /*int Usedindex = getIndexOfElementUsed();
  elementType UsedET = getETOfElementUsed();
  
  if(UsedET == allInventoryItems && Usedindex == 32)//knife
  {
    
  }*/
}

function region1_WalksOnto()
{
  if(!oFenceDoor.Visible)
  {
    player.ChangeRoom(15, 282, 107, eDirectionLeft);
  }
}

function region1_Standing()
{
  if(!oFenceDoor.Visible)
  {
    player.ChangeRoom(15, 282, 107, eDirectionLeft);
  }
}

function room_LeaveLeft()
{
  player.ChangeRoom(15, 293, 196, eDirectionLeft);
}

function region2_WalksOnto()
{
  player.ChangeRoom(16, 25, 167, eDirectionRight);
}

function room_LeaveBottom()
{
  player.ChangeRoom(6, 110, 180, eDirectionDown);
}
