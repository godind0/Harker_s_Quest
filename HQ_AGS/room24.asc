// room script file
int nbOfPropertiesBeforeDracula = 2;
String currentRequester;

bool highjackParseCommand(String cmd)
{
  bool sendBackToNormalParsing = true;
  Set* sellerList;
  int nbNegatives = getNbNegative();
  String nbNegativesMsg = "I have ### negative@ left";
  sellerList = getHawkinsSellerList();
  String sellerName;
  Object* properDrawer;
  
  if(findVerbInDictionary(cmd) == "take" && cmd.IndexOf("camera") != -1 && !player.HasInventory(iCamera)) // take camera
  {
    if(getStoryBeat() < packYourThings) {
      cHawkins.Say("Don't touch the camera, it is for office use only");
      cHawkins.Say("Get back to work");      
    } else
    {
      player.Think("This will be useful");
      player.AddInventory(iCamera);
      oCamera.Visible = false;
      switch(nbNegatives)
      {
        case 0:
          nbNegativesMsg = nbNegativesMsg.Replace("###", "no", eCaseInsensitive);
          nbNegativesMsg = nbNegativesMsg.Replace("@", "s", eCaseInsensitive);
          break;
        case 1:
          nbNegativesMsg = nbNegativesMsg.Replace("###", "1", eCaseInsensitive);
          nbNegativesMsg = nbNegativesMsg.Replace("@", "", eCaseInsensitive);
          break;
        default:
          nbNegativesMsg = nbNegativesMsg.Replace("###", String.Format("%d", nbNegatives), eCaseInsensitive);
          nbNegativesMsg = nbNegativesMsg.Replace("@", "s", eCaseInsensitive);
          break;
      }
      iCamera.SetTextProperty("TextDescription", String.Format("This is the office's photo camera. State of the art technology![%s", nbNegativesMsg));
      giveScoreOnce("GetCamera", 1);
      Display("You can now take pictures of anything in your surroundings by writing the verb 'Photograph' followed by the name of your subject");
      Display("Careful to not spend all your negatives!");
      addToVerbDictionary("photograph");
    }
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackIndexElementVerb(int index, elementType eT, String verb)
{
  bool sendBackToNormalParsing = true;
  if(eT == roomObjects && index == 50 && verb == "take")//take camera
  {
    if(getStoryBeat() < packYourThings) {
      cHawkins.Say("Don't touch the camera, it is for office use only");
      cHawkins.Say("Get back to work");      
    } else
    {
      player.Think("This will be useful");
      object[51].Visible = false;
      player.AddInventory(iCamera);
      giveScoreOnce("GetCamera", 1);
    }
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackFailedInteract(String cmd)
{
  bool sendBackToNormalParsing = true;
  
  return sendBackToNormalParsing;
}

function highjackAfterInteract(int index, elementType eT, String verb)
{
  
}

int getConveyancingQuestNumber()
{
  int thisQuestNumber;
  switch(nbOfPropertiesBeforeDracula)
  {
    case 0: // Dracula
      thisQuestNumber = 4;
      break;
    case 1:
      thisQuestNumber = 3;
      break;
    case 2:
      thisQuestNumber = 2;
      break;
    default:
      break;
  }
  
  return thisQuestNumber;
}

function setupConveyancingSolvedQuest()
{
  int thisConvQNb = getConveyancingQuestNumber();
  String thisSellerName = getProposedSeller();
  int propAge = getPropertyAge(thisSellerName);
  
  addDynamicQuestTextElement(thisConvQNb, "SellerProperty age", String.Format("%d", propAge));
  addDynamicQuestTextElement(thisConvQNb, "Seller name", thisSellerName);
  
  modifyQuestNoteLineVariant(thisConvQNb, 1, 2);
}

function giveAssignment(bool sittingWithClient)
{
  String clientName = getPropertyRequesterName(nbOfPropertiesBeforeDracula);
  String requestMessage = getPropertyRequestMessage(clientName);
  bool firstAssigned = false;
  int questNb = getConveyancingQuestNumber();
  
  if(clientName != currentRequester)
  {
    cHawkins.Say("I have a new assignment for you");
    firstAssigned = true;
  }
  
  modifyQuestNoteLineVariant(questNb, 1, 1);
  
  if(!sittingWithClient)
  {
    if(firstAssigned)
    {
      cHawkins.Say("We have a client interested in a new property");
      cHawkins.Say(String.Format("A certain %s", clientName));
    }
    cHawkins.notableSay(String.Format("(B) %s", clientName),  requestMessage, 1, (questNb-2));
    if(firstAssigned)
    {
      cHawkins.Say("Did you get that? You should note that!");
    } else
    {
      cHawkins.Say("Note it down this time!");
    }
  } else
  {
    if(firstAssigned)
    {
      cRandomMan1.setColoredView(walkView);
      //cRandomMan1.UnlockView(eStopMoving);
      if(getQuestLineVariant(2, 2) == 1)//Jonathan saw the next client
      {
        modifyQuestNoteLineVariant(2, 2, 0);
        cRandomMan1.ChangeRoom(24, hEntrance.WalkToX, hEntrance.WalkToY, eDirectionRight);
        cRandomMan1.PlaceOnWalkableArea();
      } else {
        cRandomMan1.makeSecCharAppearance(2, -1, eDirectionRight, true);
      }
      cRandomMan1.sit(oVisitorChair2, false, 399.9);
      cHawkins.Say(String.Format("Jonathan, meet Mr. %s", clientName));
      if(!getPreviouslyTalkedTo(cRandomMan1))
      {
        cRandomMan1.Say("How do you do?");
        player.Say(String.Format("It is a pleasure, Mr %s", clientName));
        player.Say("I am Jonathan Harker, Mr. Hawkins' clerk");
        cRandomMan1.Say("Very well");
      } else
      {
        player.Say("We had the pleasure to meet in the waiting room");
        cRandomMan1.Say("Indeed");
      }
    }
    cHawkins.notableSay(String.Format("(B) %s", clientName),  requestMessage, 1, (questNb-2));
    if(!firstAssigned)
    {
      cHawkins.Say(String.Format("Now, stop wasting Mr. %s's time and find him a suitable property", clientName));
    }
  }
}

function aboutThatRequest()
{
  if(isItBeforeThisTime(4,0,0,true,1))//Before 4PM
  {
    switch(nbOfPropertiesBeforeDracula)
    {
      case 0:
        giveAssignment(false);
        if(Game.DoOnceOnly("LightningAtHawkins"))
        {
          oLightning.Visible = true;
          modifyCloudiness(30);
          Wait(10);
          oLightning.Visible = false;
          cHawkins.Say("The weather just took a turn it seems. . .");
          cHawkins.Say("No matter. . .");
        }
        break;
      case 1:
        giveAssignment(true);
        break;
      default:
        giveAssignment(false);
        break;
    }
    currentRequester = getPropertyRequesterName(nbOfPropertiesBeforeDracula);
  } else {
    cHawkins.Say("You know what, that is all for today, Harker");
    cHawkins.Say("Dismissed");
    gameIsOver("Jonathan took too long to do his job and missed his date with destiny!");
  }
}

int minutesLeftForPhotos(String targetTime, int additionalDays)
{
  int nowInMinutes = translateTimeToMinutes(whatTimeIsIt(false)) + (currentDay * 24 * 60);
  int targetTimeInMinutes = translateTimeToMinutes(targetTime);
  targetTimeInMinutes = targetTimeInMinutes + (additionalDays * 24 * 60);
  
  return targetTimeInMinutes - nowInMinutes;
}

function on_call(int indexOC)
{
  String indexElementVerbHandled[];
  String cmd;
  int elementIndex;
  elementType eT;
  String verb;
  bool handled = false;
  String clientName;
  String suggestedCode;
  String requestMessage;
  
  switch(indexOC)
  {
    case 1://highjack or interact
      indexElementVerbHandled = new String[3];
      cmd = tbParser.Text;
      hide_parser_gui();  
      if(highjackParseCommand(cmd)) 
      {
        indexElementVerbHandled = roomSpecificParse(cmd);
        elementIndex = indexElementVerbHandled[0].AsInt;
        eT = stringToET(indexElementVerbHandled[1]);
        verb = indexElementVerbHandled[2];             
        if(highjackIndexElementVerb(elementIndex, eT, verb))
        {
          if(verb != "Skip")
          {
            if(verb != "Null") 
            {
              handled = interact(indexElementVerbHandled);
            }
          } else {
            handled = true;
          }
          
          if(handled) {
            highjackAfterInteract(elementIndex, eT, verb);
          } else {
            if(highjackFailedInteract(cmd))
            {
              player.Think("Hmmm. . . I do not quite get what the goal is, here");
            }
          }
        }  
      }
      break;
    case 2://New assignment
      cHawkins.Say(String.Format("On today's agenda, we have to match %d potential buyers with our sellers", nbOfPropertiesBeforeDracula + 1));
      aboutThatRequest();
      break;
    case 3://validate
      clientName = getProposedSeller();
      if(validatePropertyProposition(currentRequester,  clientName))
      {
        giveScoreOnce(clientName, 2);
        setupConveyancingSolvedQuest();
        switch(nbOfPropertiesBeforeDracula)
        {
          case 0://Dracula
            cHawkins.Say("Great suggestion!");
            cHawkins.Say("Purfleet is not proper London, but close enough");
            cHawkins.Say("This is remarkable work!");
            cHawkins.Say("I'll tell you what: How about you finish this file");
            cHawkins.Say("This client is an important referral from London");
            cHawkins.Say("He wants pictures of the property and a personnal visit to his estate in Transylvania");
            cHawkins.Say("My joints are killing me, so I should not travel too much");
            cHawkins.Say(String.Format("You go to London, well Purfleet, to that %s property",  clientName));
            cHawkins.Say("Take the pictures!");
            cHawkins.Say("We need photos of the living room, the kitchen, the dining room and the bedroom, by 4PM tomorrow", 0);
            cHawkins.Say("Bring the photo camera, it is on the table, under my portrait.");
            cHawkins.Say("Hopefully, I will be better tomorrow and can pick them up on my way to the mainland");
            cHawkins.Say("I had a reservation for tonight at the Hummums, in Covent-garden. Use it");
            cHawkins.Say("Remember: You need to be done with all of this tomorrow at 4:00PM");
            cHawkins.Say("And I want a telegram before I take the train");
            addTimedEventInXMinutes(minutesLeftForPhotos(translateTimeToHHMMAM(4, 15, "PM"), 1), "Max time for photos & telegram");
            dTrainTicketClerk.SetOptionState(1, eOptionOn);
            addTravelDestination(ExeterTrainStation);
            cHawkins.Say("Go pack your trunk, Harker, you're headed to London tonight!");
            player.Think("That is a lot of things to remember");
            player.Think("I'll take a note of all of that right away");
            takeNote();
            createANote("Assignment", "Hawkins's assignment");
            linkNoteToQuestByTitle("Assignment", 5);
            modifyQuestNoteLineVariant(5, 0, 1);
            break;
          case 1://sitting client
            player.Say(String.Format("It is in %s %s, like you asked", getPropertyRequestCity(currentRequester), getPropertyRequestSector(currentRequester)));
            player.Say("It was built in %d, hence it is %d years old, which is within your range", 1893 - getPropertyAge(getProposedSeller()), getPropertyAge(getProposedSeller()));
            cRandomMan1.Say("That sounds like a terrific place![Thank you so much, gentlemen!");
            cHawkins.Say("I'll messenger you the details");
            cRandomMan1.Say("Terrific[G'day!");
            cHawkins.Say("G'day!");
            player.Say("G'day!");
            cRandomMan1.standUp();
            cRandomMan1.Walk(hEntrance.WalkToX, hEntrance.WalkToY, eBlock, eWalkableAreas);
            cRandomMan1.Transparency = 100;
            nbOfPropertiesBeforeDracula--;
            aboutThatRequest();
            break;
          default:
            cHawkins.Say("Great suggestion!");
            cHawkins.Say("I'll send them the details!");
            nbOfPropertiesBeforeDracula--;
            aboutThatRequest();
            break;
        }
      } else
      {
        cHawkins.Say("Stop wasting time, Harker!");
        GiveScore(-1);
      }
      break;
    case 4://repeat
      aboutThatRequest();
      GiveScore(-1);
      break;
    case 5://skip trial research
      nbOfPropertiesBeforeDracula = 0;
      break;
    default:
      break;      
  }
}
function room_FirstLoad()
{
  if(getSkipCutscene())
  {
    nbOfPropertiesBeforeDracula = 0;
  }
  currentRequester = "";
  cHawkins.sit(oSollicitorChair, true);
}

function room_RepExec()
{
  
}

function region1_WalksOnto()
{
  player.ChangeRoom(2, 235, 145, eDirectionLeft);
}

function region2_WalksOnto()
{
  player.ChangeRoom(23, 265, 156, eDirectionUp);
}
