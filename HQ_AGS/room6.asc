// room script file
bool touchHorse = false;

bool highjackParseCommand(String cmd)
{
  bool sendBackToNormalParsing = true;
  if(cmd.StartsWith("exit", eCaseInsensitive) && player.Transparency == 100)
  {
    player.Move(cCoach.x, cCoach.y, eBlock, eAnywhere);
    player.PlaceOnWalkableArea();
    player.Transparency = 0;
    sendBackToNormalParsing = false;
  }
  return sendBackToNormalParsing;
}

bool highjackIndexElementVerb(int index, elementType eT, String verb)
{
  bool sendBackToNormalParsing = true;
  if(eT == allCharacters && index == 6 && verb == "talk")
  {
    if(isCharacterNearCharacter(player, 40.0, cCabby))
    {
      if(Game.DoOnceOnly("nelly needs help"))
      {
        player.Walk(cCabby.x, cCabby.y, eBlock, eWalkableAreas);
        player.FaceCharacter(cCabby);
        player.Say("What seems to be the problem?");
        cCabby.Say("'Tis ol' Nelly, my horse... She hurt herself on that uneven road...");
        player.Say("I see");
        cCabby.Say("I don't think we're gonna make it to your destination, mate");
        player.Say("I see");
        //cCabby.Say("Just making it back to London will require some painkiller and a bandage");
          cCabby.Say("Just making it back to London will require some fixing up");
        player.FaceLocation(102, 135, eBlock);
        player.Say("Maybe someone in this old house can help");
        cCabby.Say("In that place?");
        if(Game.DoOnceOnly("darker carfax"))
        {
          gDark.Transparency = gDark.Transparency - 15;
        }
        cCabby.Say("That the ol' Carfax place, mate");
        cCabby.Say("Nobody lives there");
        player.Say("Really...");
        player.Say("Is it on the market?");
        cCabby.Say("Ah! Who would want to get that old creepy thing");
        //cCabby.Say("Maybe the Asylum has what we need");
        //cCabby.Say("Can you go and ask 'em, mate?");
          cCabby.Say("Altough, I am fresh out of first aid stuff...");
          cCabby.Say("We need to find something to soothes her down");
        player.FaceCharacter(cCabby);
        player.Say("Huh?");
        //cCabby.Say("The Asylum, mate. Can you get their help?");
      }
        cCabby.Say("Go check out the ol' Carfax, maybe there is something left in there");
        cCabby.Say("Just don't get yourself killed");
      cCabby.Say("I need to keep her calm, or she'll hurt herself more");
      player.Say("Right, of course");
    } else
    {
      player.Say("He seems preoccupied with the horse");
      player.Say("Maybe if I were closer");
    }
    sendBackToNormalParsing = false;
  } else if(eT == allCharacters && index == 7 && verb == "talk")//horse
  {
    if(isCharacterNearCharacter(player, 40.0, cHorse1))
    {
      player.Walk(cHorse1.x - 30, cHorse1.y, eBlock, eWalkableAreas);
      player.Say("Here, here...");
      if(!touchHorse)
      {
        cHorse1.Say("NEIGH!");
        cCabby.Say("Whoa, Nelly!");
        cCabby.Say("I'd keep away, mate. She's not in a mood");
        player.Say("Right, my apologies");
        touchHorse = true;
      } else
      {
        cCabby.Say("Oh, brother...");
        cHorse1.Say("NEIGH!!");
        gameIsOver("Jonathan got kicked in the face");
      }
    }
  }
  return sendBackToNormalParsing;
}

bool highjackFailedInteract(String cmd)
{
  bool sendBackToNormalParsing = true;
  //insert highjacking formulas here (and set the bool to false)
  return sendBackToNormalParsing;
}


function highjackAfterInteract(int index, elementType eT, String verb)
{
  if(eT == roomHotspots && index == 1 && verb == "look")//sign
  {
    player.Say("How convient!");
    notableSay(player, "Carfax Seller", String.Format("I have the contact information of a certain %s for the sale of the 'Carfax' property in Purfleet", carfaxSeller), 3);
  }
}

function on_call(int index)
{
  String indexElementVerbHandled[];
  String cmd;
  int elementIndex;
  elementType eT;
  String verb;
  bool handled = false;
  
  switch(index)
  {
    case 1://highjack or interact
      indexElementVerbHandled = new String[3];
      cmd = tbParser.Text;
      close_gui(gParser);
      
      if(highjackParseCommand(cmd)) 
      {
        indexElementVerbHandled = roomSpecificParse(cmd);
        elementIndex = indexElementVerbHandled[0].AsInt;
        eT = stringToET(indexElementVerbHandled[1]);
        verb = indexElementVerbHandled[2];
        //debugDisplay(String.Format("Element type: %s[Index: %d[Verb: %s", etToString(eT), elementIndex, verb));
        
        if(highjackIndexElementVerb(elementIndex, eT, verb))
        {
          if(verb != "Skip")
          {
            if(verb != "Null") 
            {
              handled = interact(indexElementVerbHandled);
            }
          } else {
            handled = true;
          }
          
          if(handled) {
            highjackAfterInteract(elementIndex, eT, verb);
          } else {
            if(highjackFailedInteract(cmd))
            {
              player.Think("Hmmm... I do not quite get what the goal is, here");
            }
          }
        }
        break;
      }
  }
}
function room_FirstLoad()
{
  triggerCutscene(arriveInPurfleet);
  cHorse1.SetTextProperty("TextDescription", "Poor creature, she look hurt. She has a gash in the leg and clearly is in pain");
}

function room_LeaveTop()
{
  player.ChangeRoom(14, 216, 190, eDirectionUp);
}
