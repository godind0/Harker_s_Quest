// room script file
bool touchHorse = false;

bool highjackParseCommand(String cmd)
{
  bool sendBackToNormalParsing = true;
  Hotspot* theSign;
  bool signAtAll = false;
  
  if(cmd.IndexOf("Sanitarium") != -1 || cmd.IndexOf("Asylum") != -1)
  {
    player.Walk(334, 177, eBlock, eWalkableAreas);
    player.FaceDirection(eDirectionUp);
    player.Say("This looks like a private house, albeit a very large one, that was converted into a private sanitarium");
    sendBackToNormalParsing = false;
  } else if (cmd.IndexOf("read ") != -1 || cmd.IndexOf("look ") != -1)//look at the signs
  {
    if(cmd.IndexOf("sale") != -1)
    {
      theSign = hCarfaxForSaleSign;
      signAtAll = true;
    } else if ((cmd.IndexOf("sign") != -1) || (cmd.IndexOf("pancarte") != -1))
    {
      if(calculateDistanceFromCharacter(hCarfaxForSaleSign.WalkToX, hCarfaxForSaleSign.WalkToY, player) <= calculateDistanceFromCharacter(hSanitariumSign.WalkToX, hSanitariumSign.WalkToY, player))
      {
        theSign = hCarfaxForSaleSign;
      } else
      {
        theSign = hSanitariumSign;
      }
      signAtAll = true;
    }
    if(signAtAll)
    {
      sendBackToNormalParsing = false;
      if(isCharacterNearHotspot(theSign, 70.0, player))
      {
        player.Walk(theSign.WalkToX, theSign.WalkToY, eBlock, eWalkableAreas);
        player.FaceDirection(eDirectionUp);
        player.Say(theSign.GetTextProperty("TextDescription"));
        if(theSign == hCarfaxForSaleSign)
        {
          player.Say("How convient!");
          notableSay(player, "Carfax Seller", String.Format("I have the contact information of a certain %s for the sale of the 'Carfax' property in Purfleet", getCarfaxSeller()), 3);
        }
      } else
      {
        player.Say("I cannot read it from here");
      }
    }
  }
  return sendBackToNormalParsing;
}

bool highjackIndexElementVerb(int index, elementType eT, String verb)
{
  bool sendBackToNormalParsing = true;
  if(eT == allCharacters && index == 6 && verb == "talk")
  {
    if(isCharacterNearCharacter(player, 40.0, cCabby))
    {
      if(playerHasAllCarfaxPictures())
      {
        player.Walk(110, 177, eBlock, eWalkableAreas);
        player.FaceDirection(eDirectionRight);
        cCabby.Say("Alright, mate. I got her fixed up");
        cCabby.Say("I went to the asylum next door while you were playing sneakeroo, there");
        player.Say("Oh. . . it's for work");
        cCabby.Say("Whatever you need to tell yourself, mate");
        player.Say("Ok, can we head on to the destination?");
        cCabby.Say("Nah, this fix will barely get us back to London");
        player.Say("Oh");
        cCabby.Say(String.Format("You gonna telegram ol' %s to get his house?",  getCarfaxSeller()));
        player.FaceDirection(eDirectionUp);
        player.Say("I think I'll have to");
        if(Game.DoOnceOnly("Remember the photo service at the Law Society"))
        {
          player.Say("Can you take me to a photo development store?");
          cCabby.Say("Oh I don't think Nelly would like the detour");
          cCabby.Say("There are a few photo shops around the city. . . but they're expensive. . .");
          cCabby.Say("You know what I do? The Law Society has a photo development centre, my mate works there");
          cCabby.Say("For sure, he'll think your one of their bureaucrats");
          cCabby.Say("He's not. . . super aware of what's going on");
          player.Say("That is a splendid suggestion");
          player.Say("Thank you");
          addTravelDestination(LawSocietyOffices);
          dLawSocietyReceptionist.SetOptionState(9, eOptionOn);
        }        
        cCabby.Say("Alright, we gotta go back to the city");
        cCabby.Say("Hop back in");
        player.Walk(oFakeHarker.X + 16, 183, eBlock, eWalkableAreas);
        player.FaceDirection(eDirectionUp);
        FadeOut(5);
          cCoach.ChangeRoom(5, 424, 167);
          cCabby.ChangeRoom(5, 428, 181);
          changeArrival_X_Y(5,  394,  175);
          removeTravelDestination(Purfleet);
          dCabby.SetOptionState(8, eOptionOn);
          travel(LondonTrainStation);
          if(!player.HasInventory(iPadlockKey))
          {
            giveScoreOnce("Put key back", 1);
          }
          if(!player.HasInventory(iChain))
          {
            giveScoreOnce("Locked the cellar", 1);
          }
      } else if(Game.DoOnceOnly("nelly needs help"))
      {
        player.Walk(110, 177, eBlock, eWalkableAreas);
        player.FaceDirection(eDirectionRight);
        player.Say("What seems to be the problem?");
        cCabby.Say("'Tis ol' Nelly, my horse. . . She hurt herself on that uneven road. . .");
        player.Say("I see");
        cCabby.Say("I don't think we're gonna make it to your destination, mate");
        cCabby.Say("Just making it back to London will require some fixin'");
        player.FaceLocation(102, 135, eBlock);
        player.Say("Maybe someone in this old house can help");
        cCabby.Say("In that place?");
        cCabby.Say("That the ol' Carfax place, mate");
        if(Game.DoOnceOnly("darker carfax"))
        {
          //gDark.Transparency = gDark.Transparency - 15;
          modifyCloudiness(15);
        }
        cCabby.Say("Nobody lives there");
        player.Say("Really. . .");
        player.Say("Is it on the market?");
        cCabby.Say("Ah! Who would want to get that old creepy thing");
        Wait(15);
        cCabby.Say("Maybe the Asylum has som'hing to help the horse");
        cCabby.Say("I'll calm Nelly down and go ask 'em");
        player.FaceCharacter(cCabby);
        player.Say("Huh?");
        cCabby.Say("I said. . .");
      }
      cCabby.Say("I need to keep her calm, or she'll hurt herself more");
      player.Say("Right, of course");
    } else
    {
      player.Say("He seems preoccupied with the horse");
      player.Say("Maybe if I were closer");
    }
    sendBackToNormalParsing = false;
  } else if(eT == allCharacters && index == 7 && verb == "talk")//horse
  {
    if(isCharacterNearCharacter(player, 40.0, cCoach))
    {
      player.Walk(cCoach.x - 30, cCoach.y, eBlock, eWalkableAreas);
      player.Say("Here, here. . .");
      if(!touchHorse)
      {
        cCoach.Say("NEIGH!");
        cCabby.Say("Whoa, Nelly!");
        cCabby.Say("I'd keep away, mate. She's not in a mood");
        player.Say("Right, my apologies");
        touchHorse = true;
      } else
      {
        cCabby.Say("Oh, brother. . .");
        cCoach.Say("NEIGH!!");
        gameIsOver("Jonathan got kicked in the face");
      }
    }
  }
  return sendBackToNormalParsing;
}

bool highjackFailedInteract(String cmd)
{
  bool sendBackToNormalParsing = true;
  //insert highjacking formulas here (and set the bool to false)
  return sendBackToNormalParsing;
}


function highjackAfterInteract(int index, elementType eT, String verb)
{
  //
}

function on_call(int index)
{
  String indexElementVerbHandled[];
  String cmd;
  int elementIndex;
  elementType eT;
  String verb;
  bool handled = false;
  
  switch(index)
  {
    case 1://highjack or interact
      indexElementVerbHandled = new String[3];
      cmd = tbParser.Text;
      close_gui(gParser);
      
      if(highjackParseCommand(cmd)) 
      {
        indexElementVerbHandled = roomSpecificParse(cmd);
        elementIndex = indexElementVerbHandled[0].AsInt;
        eT = stringToET(indexElementVerbHandled[1]);
        verb = indexElementVerbHandled[2];
        //debugDisplay(String.Format("Element type: %s[Index: %d[Verb: %s", etToString(eT), elementIndex, verb));
        
        if(highjackIndexElementVerb(elementIndex, eT, verb))
        {
          if(verb != "Skip")
          {
            if(verb != "Null") 
            {
              handled = interact(indexElementVerbHandled);
            }
          } else {
            handled = true;
          }
          
          if(handled) {
            highjackAfterInteract(elementIndex, eT, verb);
          } else {
            if(highjackFailedInteract(cmd))
            {
              player.Think("Hmmm. . . I do not quite get what the goal is, here");
            }
          }
        }
      }
      break;
  }
}

function room_FirstLoad()
{
  player.Transparency = 100;
  triggerCutscene(arriveInPurfleet);
  
}

function room_LeaveTop()
{
  player.ChangeRoom(14, 216, 190, eDirectionUp);
}

function region1_WalksOnto()
{
    player.FaceDirection(eDirectionUp);
    player.Say("That is a sanitarium, I have no business there");
    player.Walk(336, 181, eBlock, eWalkableAreas);
}

function room_Load()
{
  if(playerHasAllCarfaxPictures())
  {
    setHorseViews(BandagedHorse);
    setCoachView(Empty);
  }
}

function room_AfterFadeIn()
{
  musicInRoom(ALostInThought, 2);
  if(getHorseState() == BandagedHorse)
  {
    int lastFrame = Game.GetFrameCountForLoop(cCoach.View, cCoach.Loop);
    cCoach.Animate(cCoach.Loop, 5, eOnce, eNoBlock, eForwards, lastFrame);
  }
}
