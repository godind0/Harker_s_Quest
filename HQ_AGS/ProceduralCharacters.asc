// Procedural characters module

#define HAIR_DEF 25388  //rgb(96, 100, 96)
#define MUSTACHE_DEF 27501 //rgb(104, 108, 104)
#define BEARD_DEF 25452 //rgb(100, 110, 100)
#define SKIN_DEF 58607 //rgb(224, 156, 120)
#define SKIN_DARK_DEF 58444 //rgb(224, 136, 96)
#define COAT_DEF 512 //rgb(0, 64, 0)
#define COAT_LIGHT_DEF 736 //rgb(0, 92, 0)
#define COAT_DARK_DEF 256 //rgb(0, 32, 0)
#define NECKTIE_DEF 0 //rgb(0, 0, 0)
#define UNDERSHIRT_DEF 65535 //rgb(248, 252, 248)
#define PANTS_DEF 4290 //rgb(16, 24, 16)
#define PANTS_DARK_DEF 32 //rgb(0, 4, 0)
#define SHOES_DEF 20800 //rgb(80, 40, 0)
#define SHOES_DARK_DEF 14528 //rgb(56, 24, 0)
#define EYE_DEF 2048 //rgb(10, 0, 0)
#define BODY_PARTS_NB 8
#define CLOTHES_PIECES_NB 100

Dictionary* sprites[];
Dictionary* indexedColors;

DynamicSprite* recoloredDynamicSprites[];

Set* characterIndextoManage;

GUI* clothesChangeGUI;
Button* clothesChangeCharVisual;
Label* clothesChangeBodyPart;
Label* clothesChangeColor;

DynamicSprite* clothesChangeCharVisualSprite;
Clothes playerClothes[CLOTHES_PIECES_NB];
Dictionary* playerClothesCycle;
int clothesPartsIndexes[BODY_PARTS_NB];


String viewTypeToString(viewTypes thisViewType)
{
  switch(thisViewType)
  {
    case walkView:
      return "view_walk";
      break;
    case sitView:
      return "view_sit";
      break;
    case climbView:
      return "view_climb";
      break;
    default:
      break;
  }
}

String bodyPartsToString(bodyParts thisPart)
{
  switch(thisPart)
  {
    case BP_hair:
      return "hair";
      break;
    case BP_skin:
      return "skin";
      break;
    case BP_coat:
      return "coat";
      break;
    case BP_necktie:
      return "necktie";
      break;
    case BP_undershirt:
      return "undershirt";
      break;
    case BP_pants:
      return "pants";
      break;
    case BP_shoes:
      return "shoes";
      break;
    default:
      return "null";
      break;
  }
}

bodyParts stringToBodyParts(String thisPart)
{
  switch(thisPart)
  {
    case "hair":
      return BP_hair;
      break;
    case "skin":
      return BP_skin;
      break;
    case "coat":
      return BP_coat;
      break;
    case "necktie":
      return BP_necktie;
      break;
    case "undershirt":
      return BP_undershirt;
      break;
    case "pants":
      return BP_pants;
      break;
    case "shoes":
      return BP_shoes;
      break;
    default:
      return 0;
      break;
  }
}

int getViewIndex(this Character*, viewTypes thisViewType)
{
  String viewIndexS;
  int charID = this.ID;
  
  switch(thisViewType)
  {
    case walkView:
      viewIndexS = sprites[charID].Get(viewTypeToString(walkView));
      return viewIndexS.AsInt;
      break;
    case sitView:
      viewIndexS = sprites[charID].Get(viewTypeToString(sitView));
      return viewIndexS.AsInt;
      break;
    case climbView:
      viewIndexS = sprites[charID].Get(viewTypeToString(climbView));
      return viewIndexS.AsInt;
      break;
    default:
      break;
  }
}

function fillDefaultView(int charID, viewTypes thisViewType, int viewIndex)
{
  String viewName;
  String view_loop_frame;
  int nbLoops;
  int nbFrames;
  ViewFrame* thisFrame;
  
  Character* thisChar = character[charID];
  
  int y;
  int z;
  
  sprites[charID].Set(viewTypeToString(thisViewType), String.Format("%d", viewIndex));
  viewName = viewTypeToString(thisViewType);
  nbLoops = Game.GetLoopCountForView(thisViewType);
  for(y = 0 ; y < nbLoops ; y++)
  {
    nbFrames = Game.GetFrameCountForLoop(viewIndex, y);
    for(z = 0 ; z < nbFrames ; z++)
    {
      view_loop_frame = String.Format("%s_DEF_%d_%d", viewName, y, z);
      thisFrame = Game.GetViewFrame(viewIndex, y, z);
      sprites[charID].Set(view_loop_frame, String.Format("%d", thisFrame.Graphic));
      view_loop_frame = String.Format("%s_DEF_FLIPPED_%d_%d", viewName, y, z);
      sprites[charID].Set(view_loop_frame, String.Format("%d", thisFrame.Flipped));
    }
  }
}

function fillDefaultViews(int charID)
{
  Character* thisChar = character[charID];  
  fillDefaultView(charID, walkView, thisChar.NormalView);
  
  if(thisChar.GetProperty("sitView") != -1)
  {
    fillDefaultView(charID, sitView, thisChar.GetProperty("sitView"));
  }
  
  if(thisChar.GetProperty("climbView") != -1)
  {
    fillDefaultView(charID, climbView, thisChar.GetProperty("climbView"));
  }
}

// creates an array of dictionaries containing each sprite to use for each character
function createSpriteDictionaries()
{
  int nbChars = Game.CharacterCount;
  sprites = new Dictionary[nbChars];
  Character* thisChar;
  int i;
  
  for(i = 0 ; i < nbChars ; i++)
  {
    sprites[i] = Dictionary.Create(eNonSorted);
    thisChar = character[i];
    fillDefaultViews(i);
  }  
}

// gets a color index from the characterColors enum
int getCharacterHairColor(characterColors targetColor)
{
  if(targetColor == CC_random || targetColor == CC_skin_ivory || targetColor == CC_skin_porcelain || targetColor == CC_skin_roseBeige)
  {
    int i = Random(5);
    switch(i)
    {
      case 0:
        targetColor = CC_hair_blond;
        break;
      case 1:
        targetColor = CC_hair_brown;
        break;
      case 2:
        targetColor = CC_hair_dark;
        break;
      case 3:
        targetColor = CC_hair_grey;
        break;
      case 4:
        targetColor = CC_hair_red;
        break;
      case 5:
        targetColor = CC_hair_white;
        break;
    }
  }
  
  if(targetColor == CC_skin_band || targetColor == CC_skin_dark || targetColor == CC_skin_siena || targetColor == CC_skin_warmIvory)
  {
    int i = Random(2);
    switch(i)
    {
      case 0:
        targetColor = CC_hair_white;
        break;
      case 1:
        targetColor = CC_hair_grey;
        break;
      case 2:
        targetColor = CC_hair_dark;
        break;
    }
  }
  
  switch(targetColor)
  {
    case CC_hair_blond:
      return 65159;
      break;
    case CC_hair_brown:
      return 33410;
      break;
    case CC_hair_dark:
      return 6272;
      break;
    case CC_hair_grey:
      return 48599;
      break;
    case CC_hair_red:
      return 53953;
      break;
    case CC_hair_white:
      return 63357;
      break;
  }
}

// gets an array of color tones with different shading ([0] = pale ; [1] = skin tone ; [2] = shaded)
int[] getCharacterColorTones(characterColors targetColor)
{
  int paleToneShade[] = new int[3];
  switch(targetColor)
  {
    case CC_black:
      paleToneShade[0] = 12710;
      paleToneShade[1] = 4258;
      paleToneShade[2] = 32;
      break;
    case CC_white:
      paleToneShade[0] = 65535;
      paleToneShade[1] = 61277;
      paleToneShade[2] = 52825;
      break;
    case CC_grey:
      paleToneShade[0] = 44373;
      paleToneShade[1] = 35953;
      paleToneShade[2] = 27469;
      break;
    case CC_brown:
      paleToneShade[0] = 46121;
      paleToneShade[1] = 27269;
      paleToneShade[2] = 18851;
      break;
    case CC_green:
      paleToneShade[0] = 3394;
      paleToneShade[1] = 3073;
      paleToneShade[2] = 544;
      break;
    case CC_blue:
      paleToneShade[0] = 16958;
      paleToneShade[1] = 6397;
      paleToneShade[2] = 2167;
      break;
    case CC_red:
      paleToneShade[0] = 53248;
      paleToneShade[1] = 43008;
      paleToneShade[2] = 26624;
      break;
    case CC_yellow:
      paleToneShade[0] = 59201;
      paleToneShade[1] = 48641;
      paleToneShade[2] = 33824;
      break;
    case CC_purple:
      paleToneShade[0] = 51321;
      paleToneShade[1] = 41076;
      paleToneShade[2] = 26701;
      break;
    case CC_beige:
      paleToneShade[0] = 61075;
      paleToneShade[1] = 60942;
      paleToneShade[2] = 58663;
      break;
    case CC_orange:
      paleToneShade[0] = 64516;
      paleToneShade[1] = 64320;
      paleToneShade[2] = 47712;
      break;
    case CC_cyan:
      paleToneShade[0] = 1884;
      paleToneShade[1] = 1527;
      paleToneShade[2] = 1007;
      break;
    case CC_skin_porcelain:
      paleToneShade[0] = 65303;
      paleToneShade[1] = 65303;
      paleToneShade[2] = 65072;
      break;
    case CC_skin_ivory:
      paleToneShade[0] = 65076;
      paleToneShade[1] = 65076;
      paleToneShade[2] = 64847;
      break;
    case CC_skin_warmIvory:
      paleToneShade[0] = 65400;
      paleToneShade[1] = 65400;
      paleToneShade[2] = 65267;
      break;
    case CC_skin_roseBeige:
      paleToneShade[0] = 65008;
      paleToneShade[1] = 65008;
      paleToneShade[2] = 64616;
      break;
    case CC_skin_siena:
      paleToneShade[0] = 43914;
      paleToneShade[1] = 43914;
      paleToneShade[2] = 27011;
      break;
    case CC_skin_band:
      paleToneShade[0] = 39914;
      paleToneShade[1] = 39914;
      paleToneShade[2] = 25059;
      break;
    case CC_skin_dark:
      paleToneShade[0] = 33512;
      paleToneShade[1] = 33512;
      paleToneShade[2] = 12579;
      break;
  }
  
  return paleToneShade;
}

// pick a random jacket color among available colors
characterColors getCharacterRandomJacketColor()
{
  int i = Random(11);
  switch(i)
  {
    case 0:
      return CC_beige;
      break;
    case 1:
      return CC_black;
      break;
    case 2:
      return CC_blue;
      break;
    case 3:
      return CC_brown;
      break;
    case 4:
      return CC_cyan;
      break;
    case 5:
      return CC_green;
      break;
    case 6:
      return CC_grey;
      break;
    case 7:
      return CC_orange;
      break;
    case 8:
      return CC_purple;
      break;
    case 9:
      return CC_red;
      break;
    case 10:
      return CC_white;
      break;
    case 11:
      return CC_yellow;
      break;
  }
}

// pick a random skin color among the predefined ones
characterColors getCharacterRandomSkinColor()
{
  int i = Random(6);
  switch(i)
  {
    case 0:
      return CC_skin_band;
      break;
    case 1:
      return CC_skin_dark;
      break;
    case 2:
      return CC_skin_ivory;
      break;
    case 3:
      return CC_skin_porcelain;
      break;
    case 4:
      return CC_skin_roseBeige;
      break;
    case 5:
      return CC_skin_siena;
      break;
    case 6:
      return CC_skin_warmIvory;
      break;
  } 
}

// build a color dictionary
Dictionary* makeAColorDictionary(int hair, int mustache, int skin, int skinShadow, int coat, int coatLight, int coatDark, int neckTie, int underShirt, int pants, int pantsDark, int shoe, int shoeDark,  int beard)
{
  Dictionary* colorDict = Dictionary.Create(eSorted);
  colorDict.Set("hair", String.Format("%d", hair));
  colorDict.Set("mustache", String.Format("%d", mustache));
  colorDict.Set("beard", String.Format("%d", beard));
  colorDict.Set("skin", String.Format("%d", skin));
  colorDict.Set("skinShadow", String.Format("%d", skinShadow));
  colorDict.Set("coat", String.Format("%d", coat));
  colorDict.Set("coatLight", String.Format("%d", coatLight));
  colorDict.Set("coatDark", String.Format("%d", coatDark));
  colorDict.Set("neckTie", String.Format("%d", neckTie));
  colorDict.Set("underShirt", String.Format("%d", underShirt));
  colorDict.Set("pants", String.Format("%d", pants));
  colorDict.Set("pantsDark", String.Format("%d", pantsDark));
  colorDict.Set("shoe", String.Format("%d", shoe));
  colorDict.Set("shoeDark", String.Format("%d", shoeDark));
  
  return colorDict;
}

// change parts of a color dictionary
Dictionary* changeColorDictionary(Dictionary* initialColorDict, bodyParts partToChange, characterColors targetColor)
{
  int listColors[] = new int[3];
  int thisColor;
  
  switch(partToChange)
  {
    case BP_coat:
      listColors = getCharacterColorTones(targetColor);
      initialColorDict.Set("coatLight", String.Format("%d", listColors[0]));
      initialColorDict.Set("coat", String.Format("%d", listColors[1]));
      initialColorDict.Set("coatDark", String.Format("%d", listColors[2]));
      break;
    case BP_hair:
      initialColorDict.Set("hair", String.Format("%d", getCharacterHairColor(targetColor)));
      if(initialColorDict.Get("hair") == initialColorDict.Get("mustache"))
      {
        initialColorDict.Set("mustache", String.Format("%d", getCharacterHairColor(targetColor)));
      }
      if(initialColorDict.Get("hair") == initialColorDict.Get("beard"))
      {
        initialColorDict.Set("beard", String.Format("%d", getCharacterHairColor(targetColor)));
      }
      break;
    case BP_necktie:
      listColors = getCharacterColorTones(targetColor);
      initialColorDict.Set("neckTie", String.Format("%d", listColors[2]));
      break;
    case BP_pants:
      listColors = getCharacterColorTones(targetColor);
      initialColorDict.Set("pants", String.Format("%d", listColors[1]));
      initialColorDict.Set("pantsDark", String.Format("%d", listColors[2]));
      break;
    case BP_shoes:
      listColors = getCharacterColorTones(targetColor);
      initialColorDict.Set("shoe", String.Format("%d", listColors[1]));
      initialColorDict.Set("shoeDark", String.Format("%d", listColors[2]));
      break;      
  }
  return initialColorDict;
}

Dictionary* getColorDictionary(this Character*)
{
  if(sprites[this.ID].Contains("proceduralCharacter"))
  {
    String hairS = sprites[this.ID].Get("hair");
    String mustacheS = sprites[this.ID].Get("mustache");
    String skinS = sprites[this.ID].Get("skin");
    String skinShadowS = sprites[this.ID].Get("skinShadow");
    String coatS = sprites[this.ID].Get("coat");
    String coatLightS = sprites[this.ID].Get("coatLight");
    String coatDarkS = sprites[this.ID].Get("coatDark");
    String neckTieS = sprites[this.ID].Get("neckTie");
    String undershirtS = sprites[this.ID].Get("underShirt");
    String pantsS = sprites[this.ID].Get("pants");
    String pantsDarkS = sprites[this.ID].Get("pantsDark");
    String shoeS = sprites[this.ID].Get("shoe");
    String shoeDarkS = sprites[this.ID].Get("shoeDark");
    String beardS = sprites[this.ID].Get("beard");
    
    return makeAColorDictionary(hairS.AsInt, mustacheS.AsInt, skinS.AsInt, skinShadowS.AsInt, coatS.AsInt, coatLightS.AsInt, coatDarkS.AsInt, neckTieS.AsInt, undershirtS.AsInt, pantsS.AsInt, pantsDarkS.AsInt, shoeS.AsInt, shoeDarkS.AsInt, beardS.AsInt);
  } else
  {
    Dictionary* falseDict = Dictionary.Create(eNonSorted);
    falseDict.Set("error", "error");
    return falseDict;
  }
}

// makes the 'default' color dictionary,  based on the default sprites' color
Dictionary* makeDefaultColorDictionary()
{
  return makeAColorDictionary(HAIR_DEF, MUSTACHE_DEF, SKIN_DEF, SKIN_DARK_DEF, COAT_DEF, COAT_LIGHT_DEF, COAT_DARK_DEF, NECKTIE_DEF, UNDERSHIRT_DEF, PANTS_DEF, PANTS_DARK_DEF, SHOES_DEF, SHOES_DARK_DEF, BEARD_DEF);
}

int addRecoloredDynamicSprite(int charID, DynamicSprite* thisDynamicSprite)
{
  int arrayIndex;
  
  if(!sprites[charID].Contains("recoloredDynamicSpritesLastIndex"))
  {
    arrayIndex = (1000 + (charID * 100));
  } else
  {
    String arrayIndexS = sprites[charID].Get("recoloredDynamicSpritesLastIndex");
    arrayIndex = arrayIndexS.AsInt;
    arrayIndex++;
  }
  
  recoloredDynamicSprites[arrayIndex] = thisDynamicSprite;
  
  sprites[charID].Set("recoloredDynamicSpritesLastIndex", String.Format("%d", arrayIndex));
  
  return arrayIndex;
}

function deleteCharactersRecoloredDynamicSprites(int charID)
{
  int firstIndex;
  String lastIndexS;
  int lastIndex;
  if(sprites[charID].Contains("recoloredDynamicSpritesLastIndex"))
  {
    lastIndexS = sprites[charID].Get("recoloredDynamicSpritesLastIndex");
    lastIndex =  1 + lastIndexS.AsInt;
    for(firstIndex = (1000 + (charID * 100)) ; firstIndex < lastIndex ; firstIndex++)
    {
      if(recoloredDynamicSprites[firstIndex] != null)
      {
        recoloredDynamicSprites[firstIndex].Delete();
      }
    }
    sprites[charID].Remove("recoloredDynamicSpritesLastIndex");
  }
}

bool hasView (this Character*, viewTypes thisViewType)
{
  return sprites[this.ID].Contains(viewTypeToString(thisViewType));
}

// creates a dictionary for a character recolor,  use CC_random to chose random color EXCEPT for the mustache (then use -1)
Dictionary* createASecondaryNPCDictionary(characterColors thisSkin, characterColors thisHair, int hasMustache, int hasBeard, characterColors thisJacket, characterColors thisNecktie, characterColors thisUndershirt, characterColors thisPant, characterColors thisShoe)
{
  int finalHair;
  int finalSkin[];
  int finalMustache;
  int finalBeard;
  int finalJacket[];
  int finalNecktie[];
  int finalUndershirt[];
  int finalPants[];
  int finalShoes[];
  
  // SKIN
  if(thisSkin == CC_random)
  {
    thisSkin = getCharacterRandomSkinColor();
  }
  finalSkin = getCharacterColorTones(thisSkin);
  
  // HAIR
  if(thisHair == CC_random)
  {
    thisHair = thisSkin;
  }
  finalHair = getCharacterHairColor(thisHair);
  
  // MUSTACHE
  if(hasMustache == -1)
  {
    hasMustache = Random(1);
  }
  if(hasMustache == 0)
  {
    finalMustache = finalSkin[1];
  } else
  {
    finalMustache = finalHair;
  }
  
  // BEARD
  if(hasBeard == -1)
  {
    hasBeard = Random(1);
  }
  if(hasBeard == 0)
  {
    finalBeard = finalSkin[1];
  } else
  {
    finalBeard = finalHair;
  }
  
  // JACKET
  if(thisJacket == CC_random)
  {
    thisJacket = getCharacterRandomJacketColor();
  }
  finalJacket = getCharacterColorTones(thisJacket);
  
  // NECKTIE
  if(thisNecktie == CC_random)
  {
    int i = Random(1);
    if(i == 0)
    {
      thisNecktie = thisJacket;
    } else
    {
      thisNecktie = CC_black;
    }
  }
  finalNecktie = getCharacterColorTones(thisNecktie);
  
  // UNDERSHIRT
  if(thisUndershirt == CC_random)
  {
    thisUndershirt = CC_white;
  }
  finalUndershirt = getCharacterColorTones(thisUndershirt);
  
  // PANTS
  if(thisPant == CC_random)
  {
    int i = Random(5);
    switch(i)
    {
      case 0:
        thisPant = thisJacket;
        break;
      case 1:
        thisPant = CC_beige;
        break;
      case 2:
        thisPant = CC_black;
        break;
      case 3:
        thisPant = CC_brown;
        break;
      case 4:
        thisPant = CC_grey;
        break;
      case 5:
        thisPant = CC_white;
        break; 
    }
  }
  finalPants = getCharacterColorTones(thisPant);
  
  // SHOES
  if(thisShoe == CC_random)
  {
    int i = Random(6);
    switch(i)
    {
      case 0:
        thisShoe = thisJacket;
        break;
      case 1:
        thisShoe = CC_beige;
        break;
      case 2:
        thisShoe = CC_black;
        break;
      case 3:
        thisShoe = CC_brown;
        break;
      case 4:
        thisShoe = CC_grey;
        break;
      case 5:
        thisShoe = CC_white;
        break;
      case 6:
        thisShoe = thisPant;
        break; 
    }
  }
  finalShoes = getCharacterColorTones(thisShoe);
  
  return makeAColorDictionary(finalHair, finalMustache, finalSkin[1], finalSkin[2], finalJacket[1], finalJacket[0], finalJacket[2], finalNecktie[2], finalUndershirt[0], finalPants[1], finalPants[2], finalShoes[1], finalShoes[2], finalBeard);
}

// uses the createASecondaryNPC function with only the randomized parameters
Dictionary* createARandomSecondaryNPCDictionary()
{
  return createASecondaryNPCDictionary(CC_random, CC_random, -1, -1, CC_random, CC_random, CC_random, CC_random, CC_random);
}

String getCharColorType(int colorCode)
{
  String colorCodeS = String.Format("%d", colorCode);
  return indexedColors.Get(colorCodeS);
}

int getCharacterColorCode(String colorType,  Dictionary* newColors)
{
  colorType = newColors.Get(colorType);
  int colorCode = colorType.AsInt;
  return colorCode;
}

DynamicSprite* recolorSprite(int thisFrameGraphic,  Dictionary* colors)
{
  DynamicSprite* thisDynamicSprite;
  DrawingSurface* thisSurface;
  int surfaceWidth;
  int surfaceHeight;
  int surfaceX;
  int surfaceY;
  int surfaceXY_colorCode;
  String surfaceXY_colorType;
  
  thisDynamicSprite = DynamicSprite.CreateFromExistingSprite(thisFrameGraphic, true);   //create the new sprite
      
  thisSurface = thisDynamicSprite.GetDrawingSurface();
  surfaceHeight = thisSurface.Height;
  surfaceWidth = thisSurface.Width;
  
  for(surfaceX = 0 ; surfaceX < surfaceWidth ; surfaceX++)                              //look at each pixel
  {
    for(surfaceY = 0 ; surfaceY < surfaceHeight ; surfaceY++)
    {
      surfaceXY_colorCode = thisSurface.GetPixel(surfaceX, surfaceY);
      if(surfaceXY_colorCode > -1)                                                      //if it's not transparent, figure out what body part it is based on our default color codes                          
      {
        surfaceXY_colorType = getCharColorType(surfaceXY_colorCode);
        if(surfaceXY_colorType != "" && surfaceXY_colorType != "eyes")                  //if it is a body part
        {
          thisSurface.DrawingColor = getCharacterColorCode(surfaceXY_colorType, colors);//paint it the right color
          thisSurface.DrawPixel(surfaceX, surfaceY);
        }
      }
    }
  }
  
  return thisDynamicSprite;
}

function setupRecoloredView(Character* thisChar, viewTypes thisViewType,  Dictionary* colors)
{
  String viewName;
  int thisView;
  int nbLoops;
  int nbFrames;
  String thisFrameGraphicS;
  int thisFrameGraphic;
  ViewFrame* thisFrame;
  
  int charID = thisChar.ID;
  String view_loop_frame;
  
  int graphicSlot;
  DynamicSprite* thisDynamicSprite;
  int thisDynamicSpriteGraphic;
  
  thisView = thisChar.getViewIndex(thisViewType);
  viewName = viewTypeToString(thisViewType);
  nbLoops = Game.GetLoopCountForView(thisView);                                             //Get total loops in the current character view
  
  int y;
  int z;
  
  for(y = 0 ; y < nbLoops ; y++)                                                            //For each loop...
  {
    nbFrames = Game.GetFrameCountForLoop(thisView, y);                                      //Get the number of frames in the current loop
    for(z = 0 ; z < nbFrames ; z++)                                                         //For each frame in the current loop
    {
      thisFrameGraphicS = sprites[charID].Get(String.Format("%s_DEF_%d_%d", viewName, y, z)); //Get the frame...
      thisFrameGraphic = thisFrameGraphicS.AsInt;
      thisDynamicSprite = recolorSprite(thisFrameGraphic, colors);
      
      view_loop_frame = String.Format("%s_RECOLORED_%d_%d", viewName, y, z);               //define a new dictionary name for it
      sprites[charID].Set(view_loop_frame, String.Format("%d", thisDynamicSpriteGraphic));//insert the proper number
      thisFrame = Game.GetViewFrame(thisView, y, z);
      thisDynamicSpriteGraphic = addRecoloredDynamicSprite(charID, thisDynamicSprite);
      thisFrame.Graphic = recoloredDynamicSprites[thisDynamicSpriteGraphic].Graphic;
    }
  }
}

function recolor(this Character*, Dictionary* colors)
{
  deleteCharactersRecoloredDynamicSprites(this.ID);
  
  sprites[this.ID].Set("proceduralCharacter", "true");
  sprites[this.ID].Set("hair", colors.Get("hair"));
  sprites[this.ID].Set("mustache", colors.Get("mustache"));
  sprites[this.ID].Set("beard", colors.Get("beard"));
  sprites[this.ID].Set("skin", colors.Get("skin"));
  sprites[this.ID].Set("skinShadow", colors.Get("skinShadow"));
  sprites[this.ID].Set("coat", colors.Get("coat"));
  sprites[this.ID].Set("coatLight", colors.Get("coatLight"));
  sprites[this.ID].Set("coatDark", colors.Get("coatDark"));
  sprites[this.ID].Set("neckTie", colors.Get("neckTie"));
  sprites[this.ID].Set("underShirt", colors.Get("underShirt"));
  sprites[this.ID].Set("pants", colors.Get("pants"));
  sprites[this.ID].Set("pantsDark", colors.Get("pantsDark"));
  sprites[this.ID].Set("shoe", colors.Get("shoe"));
  sprites[this.ID].Set("shoeDark", colors.Get("shoeDark"));
  
  setupRecoloredView(this, walkView, colors);
  if(this.hasView(sitView))
  {
    setupRecoloredView(this, sitView, colors);
  }
  if(this.hasView(climbView))
  {
    setupRecoloredView(this, climbView, colors);
  }  
}

function recolorPart(this Character*, bodyParts partToChange, characterColors targetColor)
{
  Dictionary* charColorDict = this.getColorDictionary();
  Dictionary* sentDict;
  if(!charColorDict.Contains("error"))
  {
    sentDict = changeColorDictionary(charColorDict, partToChange, targetColor);
    this.recolor(sentDict);
  }
}

function removeFacialHair(this Character*, bool removeMustache, bool removeBeard)
{
  Dictionary* charColorDict = this.getColorDictionary();
  
  if(!charColorDict.Contains("error"))
  {
    if(removeMustache)
    {
      charColorDict.Set("mustache", charColorDict.Get("skin"));
    }
    if(removeBeard)
    {
      charColorDict.Set("beard", charColorDict.Get("skin"));
    }
  
    this.recolor(charColorDict);
  }
}

function addFacialHair(this Character*, bool addMustache, bool addBeard, int manuallyEnteredColor)
{
  Dictionary* charColorDict = this.getColorDictionary();
  
  if(!charColorDict.Contains("error"))
  {
    if(addMustache)
    {
      charColorDict.Set("mustache", String.Format("%d", manuallyEnteredColor));
    }
    if(addBeard)
    {
      charColorDict.Set("beard", String.Format("%d", manuallyEnteredColor));
    }
  
    this.recolor(charColorDict);
  }
}

function recolorPartManualEntry(this Character*, bodyParts partToChange, int manuallyEnteredColor, int manuallyEnteredColorLight, int manuallyEnteredColorDark)
{
  Dictionary* charColorDict = this.getColorDictionary();
  
  if(manuallyEnteredColorDark == -1)
  {
    manuallyEnteredColorDark = manuallyEnteredColor;
  }
  
  if(manuallyEnteredColorLight == -1)
  {
    manuallyEnteredColorLight = manuallyEnteredColor;
  }
  
  if(!charColorDict.Contains("error"))
  {
    switch(partToChange)
    {
      case BP_hair:
        charColorDict.Set("hair", String.Format("%d", manuallyEnteredColor));
        if(charColorDict.Get("hair") == charColorDict.Get("mustache"))
        {
          charColorDict.Set("mustache", String.Format("%d", manuallyEnteredColor));
        }
        if(charColorDict.Get("hair") == charColorDict.Get("beard"))
        {
          charColorDict.Set("beard", String.Format("%d", manuallyEnteredColor));
        }
        break;
      case BP_skin:
        charColorDict.Set("skin", String.Format("%d", manuallyEnteredColor));
        charColorDict.Set("skinShadow", String.Format("%d", manuallyEnteredColorDark));
        break;
      case BP_coat:
        charColorDict.Set("coat", String.Format("%d", manuallyEnteredColor));
        charColorDict.Set("coatLight", String.Format("%d", manuallyEnteredColorLight));
        charColorDict.Set("coatDark", String.Format("%d", manuallyEnteredColorDark));
        break;
      case BP_necktie:
        charColorDict.Set("neckTie", String.Format("%d", manuallyEnteredColor));
        break;
      case BP_undershirt:
        charColorDict.Set("underShirt", String.Format("%d", manuallyEnteredColor));
        break;
      case BP_pants:
        charColorDict.Set("pants", String.Format("%d", manuallyEnteredColor));
        charColorDict.Set("pantsDark", String.Format("%d", manuallyEnteredColorDark));
        break;
      case BP_shoes:
        charColorDict.Set("shoe", String.Format("%d", manuallyEnteredColor));
        charColorDict.Set("shoeDark", String.Format("%d", manuallyEnteredColorDark));
        break;
    }
    this.recolor(charColorDict);
  }
}

function recolorRandomly(this Character*)
{
  this.recolor(createARandomSecondaryNPCDictionary());
}

void makeSecCharAppearance(this Character*, int spawnHotspotIndex, int targetHotspotIndex, CharacterDirection secNPCFacing, bool randomizeAppearance)
{
  if(randomizeAppearance)
  {
    this.recolorRandomly();
  }  
  this.ChangeRoom(player.Room, hotspot[spawnHotspotIndex].WalkToX, hotspot[spawnHotspotIndex].WalkToY, secNPCFacing);
  this.PlaceOnWalkableArea();
  this.Transparency = 0;
  this.Walk(hotspot[targetHotspotIndex].WalkToX, hotspot[targetHotspotIndex].WalkToY, eNoBlock, eWalkableAreas);
  characterIndextoManage.Add(String.Format("%d", this.ID));
}

bool isClothesChangeGUISet()
{
  bool isTheGUISet = (!(clothesChangeGUI == null));
  bool isTheBtnSet = (!(clothesChangeCharVisual == null));
  return (isTheGUISet && isTheBtnSet);
}

bool isThereAtLeastOneClothingInThisSet(bodyParts set)
{
  int i = 0;
  
  while(i < CLOTHES_PIECES_NB && playerClothes[i].bodyPart != set && !playerClothes[i].available)
  {
    i++;
  }
  
  return (i < CLOTHES_PIECES_NB);
}

int getClothesIndex(bodyParts set, String colorDescription)
{
  int index = 0;
  while(index < CLOTHES_PIECES_NB && playerClothes[index].bodyPart != set && playerClothes[index].colorDescription != colorDescription) // find first available slot in the array
  {
    index++;
  }
  
  if(index < CLOTHES_PIECES_NB)
  {
    return index;
  } else
  {
    return -1;
  }
}

bool isClothesColorAvailable(bodyParts set, String colorDesc)
{
  return (getClothesIndex(set, colorDesc) != -1);
}

function removePlayerClothing(bodyParts set, String colorDescription)
{
  int index = getClothesIndex(set, colorDescription);
  
  if(index != -1)
  {
    playerClothes[index].available = false;
  }
}

function addPlayerClothing(bodyParts set, String colorDescription, int mainColor, int lightColor, int darkColor, bool available)
{
  int index = 0;
  while(index < CLOTHES_PIECES_NB && playerClothes[index].colorDescription == null) // find first available slot in the array
  {
    index++;
  }
  
  playerClothes[index].available = available;
  playerClothes[index].bodyPart = set;
  playerClothes[index].colorDescription = colorDescription;
  playerClothes[index].darkColor = darkColor;
  playerClothes[index].lightColor = lightColor;
  playerClothes[index].mainColor = mainColor;
}

String clothesChangeSetFirstBodyPart(bool coat, bool necktie, bool shirt, bool pants, bool shoe, bool hair)
{
  if(isThereAtLeastOneClothingInThisSet(BP_coat) && coat)
  {
    clothesChangeBodyPart.Text = "Coat";
    return bodyPartsToString(BP_coat);
  } else if(isThereAtLeastOneClothingInThisSet(BP_necktie) && necktie)
  {
    clothesChangeBodyPart.Text = "Necktie";
    return bodyPartsToString(BP_necktie);
  } else if(isThereAtLeastOneClothingInThisSet(BP_undershirt) && shirt)
  {
    clothesChangeBodyPart.Text = "Shirt";
    return bodyPartsToString(BP_undershirt);
  } else if(isThereAtLeastOneClothingInThisSet(BP_pants) && pants)
  {
    clothesChangeBodyPart.Text = "Pants";
    return bodyPartsToString(BP_pants);
  } else if(isThereAtLeastOneClothingInThisSet(BP_shoes) && shoe)
  {
    clothesChangeBodyPart.Text = "Shoes";
    return bodyPartsToString(BP_shoes);
  } else if(isThereAtLeastOneClothingInThisSet(BP_hair) && hair)
  {
    clothesChangeBodyPart.Text = "Hair";
    return bodyPartsToString(BP_hair);
  } else
  {
    return "null";
  }
}

bool clothesChangeSetFirstColor(bodyParts set)
{
  int i = 0;
  while(i < CLOTHES_PIECES_NB && playerClothes[i].bodyPart != set && !playerClothes[i].available)
  {
    i++;
  }
  
  if(i < CLOTHES_PIECES_NB)
  {
    clothesChangeColor.Text = playerClothes[i].colorDescription;
    return true;
  } else
  {
    return false;
  }
}

void dressUp(this Character*, int clothesIndex)
{
  //this.recolorPartManualEntry(, //HEREE !!
}

bool setupChangeClothesGUI(bool coat, bool necktie, bool shirt, bool pants, bool shoe, bool hair)
{
  Dictionary* defaultCharColorDict = makeDefaultColorDictionary();
  Dictionary* currentCharColorDict = player.getColorDictionary();
  bool success = true;
  String firstShownPart;
  int GUISprite;
  
  if(isClothesChangeGUISet())
  {
    GUISprite = clothesChangeCharVisual.Graphic;
    clothesChangeCharVisualSprite = recolorSprite(GUISprite, currentCharColorDict);
    clothesChangeCharVisual.NormalGraphic = clothesChangeCharVisualSprite.Graphic;
    firstShownPart = clothesChangeSetFirstBodyPart(coat, necktie, shirt, pants, shoe, hair);
    if(firstShownPart == "null")
    {
      success = false;
    } else
    {
      if(clothesChangeSetFirstColor(stringToBodyParts(firstShownPart)))
      {
        
      } else
      {
        success = false;
      }
    }    
  } else
  {
    success = false;
  }
}

function setClothesChangeGUI(GUI* clothesGUI, Button* charVisual, Label* bodyPartName, Label* colorName)
{
  clothesChangeGUI = clothesGUI;
  clothesChangeCharVisual = charVisual;
  clothesChangeBodyPart = bodyPartName;
  clothesChangeColor = colorName;  
}

function open_changeClothes_gui(bool coat, bool necktie, bool shirt, bool pants, bool shoe, bool hair)
{
  setupChangeClothesGUI(coat, necktie, shirt, pants, shoe, hair);
  
}

function removeRandomChars(bool allOfThemRightNow)
{
  int characterIndextoManageLength = characterIndextoManage.ItemCount;
  if(characterIndextoManageLength > 0)
  {
    String charactersToManage[] = characterIndextoManage.GetItemsAsArray();
  
    String thisCharacterIndexAsString;
    Character* thisCharacter;
    
    for(int i = 0 ; i < characterIndextoManageLength ; i++)
    {
      thisCharacterIndexAsString = charactersToManage[i];
      thisCharacter = character[thisCharacterIndexAsString.AsInt];
      if(!thisCharacter.Moving || allOfThemRightNow)
      {
        thisCharacter.Transparency = 100;
        characterIndextoManage.Remove(thisCharacterIndexAsString);
      }
    }
  }  
}

function repeatedly_execute()
{
  removeRandomChars(false);
}

// called when the game starts, before the first room is loaded
function game_start()
{
  int neededSlots = 1000 + (100 * Game.CharacterCount);
  recoloredDynamicSprites = new DynamicSprite[neededSlots];
  characterIndextoManage = Set.Create(eNonSorted);
  
  createSpriteDictionaries();
  playerClothesCycle = Dictionary.Create(eNonSorted);
  clothesPartsIndexes[BP_coat] = -1;
  clothesPartsIndexes[BP_hair] = -1;
  clothesPartsIndexes[BP_necktie] = -1;
  clothesPartsIndexes[BP_pants] = -1;
  clothesPartsIndexes[BP_shoes] = -1;
  clothesPartsIndexes[BP_skin] = -1;
  clothesPartsIndexes[BP_undershirt] = -1;
  
  indexedColors = Dictionary.Create(eSorted);
  indexedColors.Set("25388", "hair");
  indexedColors.Set("27501", "mustache");
  indexedColors.Set("25452", "beard");
  indexedColors.Set("58607", "skin");
  indexedColors.Set("58444", "skinShadow");
  indexedColors.Set("512", "coat");
  indexedColors.Set("736", "coatLight");
  indexedColors.Set("256", "coatDark");
  indexedColors.Set("0", "neckTie");
  indexedColors.Set("65535", "underShirt");
  indexedColors.Set("4290", "pants");
  indexedColors.Set("32", "pantsDark");
  indexedColors.Set("20800", "shoe");
  indexedColors.Set("14528", "shoeDark");
  indexedColors.Set("2048", "eyes");
}